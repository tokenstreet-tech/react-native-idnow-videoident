buildscript {
    if (project == rootProject) {
        repositories {
            google()
            mavenCentral()
        }

        dependencies {
            classpath 'com.android.tools.build:gradle:7.2.2'
        }
    }
}

apply plugin: 'com.android.library'

def safeExtGet(prop, fallback) {
    rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
}

android {
    compileSdkVersion safeExtGet('ReactNativeIdnowVideoident_compileSdkVersion', 31)
    defaultConfig {
        minSdkVersion safeExtGet('ReactNativeIdnowVideoident_minSdkVersion', 21)
        targetSdkVersion safeExtGet('ReactNativeIdnowVideoident_targetSdkVersion', 31)
        versionCode 1
        versionName "1.0"
    }

    buildTypes {
        release {
            minifyEnabled false
        }
    }
    lintOptions {
        disable 'GradleCompatible'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
}

repositories {
    mavenLocal()
    maven {
        // All of React Native (JS, Obj-C sources, Android binaries) is installed from npm
        url("$rootDir/../node_modules/react-native/android")
    }
    google()
    mavenCentral()
    jcenter() {
        // JCenter is now read-only. Therefore, no new versions are published there any more.
        // We only fetch the necessary dependencies for IDnow from JCenter to avoid loading old dependencies.
        content {
            includeModule("me.relex", "circleindicator")
            includeModule("com.github.barteksc", "android-pdf-viewer")
        }
    }
    maven { url "https://raw.githubusercontent.com/idnow/de.idnow.android/master" }
}

dependencies {
    //noinspection GradleDynamicVersion
    implementation "com.facebook.react:react-native:+"  // From node_modules
    implementation "de.idnow.sdk:idnow-android-sdk:5.1.12"
}
